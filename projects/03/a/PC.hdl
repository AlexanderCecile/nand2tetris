// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:

    // load, inc, reset

    Mux16(a=regout, b=reginc, sel=inc, out=regin1);
    Mux16(a=regin1, b=in, sel=load, out=regin2);
    Mux16(a=regin2, b=false, sel=reset, out=regin);
    
    Inc16(in=regout, out=reginc);

    Or(a=load, b=inc, out=regload1);
    Or(a=regload1, b=reset, out=regload);

    Register(in=regin, load=regload, out=regout, out=out);
}
